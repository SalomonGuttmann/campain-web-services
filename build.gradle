plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.1'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.rest.webservices'
version = '0.0.1-SNAPSHOT'
description = 'restful-web-services'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

configurations {
	jaxb
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'tools.jackson.dataformat:jackson-dataformat-xml'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.data:spring-data-rest-hal-explorer'
	implementation 'org.springframework.boot:spring-boot-starter-hateoas'
	implementation 'org.postgresql:postgresql'
	implementation 'jakarta.xml.bind:jakarta.xml.bind-api'
    implementation 'org.glassfish.jaxb:jaxb-runtime'
    implementation 'org.springframework.ws:spring-ws-core:5.0.0'
    implementation 'wsdl4j:wsdl4j:1.6.3'

	jaxb "org.glassfish.jaxb:jaxb-xjc:4.0.4"
	jaxb "org.glassfish.jaxb:jaxb-runtime:4.0.4"

	runtimeOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-hateoas-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-validation-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}


tasks.register('xjc') {
	description = 'Generiert Java-Klassen aus XSD'
	group = 'build'

	def xsdFile = file("src/main/resources/META-INF/schemas/campainsWs.xsd")
	def outputDir = file("src/main/java")

	inputs.file(xsdFile)
	outputs.dir(outputDir)

	doLast {
		ant.taskdef(
				name: 'xjc',
				classname: 'com.sun.tools.xjc.XJCTask',
				classpath: configurations.jaxb.asPath
		)

		ant.xjc(
				destdir: outputDir,
				schema: xsdFile,
				package: "com.rest.webservices.restfulwebservices.soap",
				extension: true
		)
	}
}

compileJava.dependsOn tasks.xjc

tasks.named('test') {
	useJUnitPlatform()
}


